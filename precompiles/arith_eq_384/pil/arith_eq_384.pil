require "std_lookup.pil"
require "std_range_check.pil"
require "arith_eq_lt_table.pil"

const int ARITH_384_MOD_OP = 0xE2;
const int EC_ADD_BLS12_381_OP = 0xE3;
const int EC_DBL_BLS12_381_OP = 0xE4;
const int COMPLEX_ADD_BLS12_381_OP = 0xE5;
const int COMPLEX_SUB_BLS12_381_OP = 0xE6;
const int COMPLEX_MUL_BLS12_381_OP = 0xE7;

airtemplate ArithEq384(const int N, const int operation_bus_id = OPERATION_BUS_ID) {
    /*
        EQ0 : x1 * y1 + x2 - x3 - q1 * y2 * p2_384 - q0 * y2  modular arith  ARITH_384_MOD      x3 = mod(x1*y1+x2, y2)
        EQ1 : s * x2 - s * x1 - y2 + y1 + (q0 * p)            lambda - ADD   EC_ADD_BLS12_381
        EQ2 : 2 * s * y1 - 3 * x1 * x1 + (q0 * p)             lambda - DBL   EC_DBL_BLS12_381
        EQ3 : s * s - x1 - x2 - x3 + (q1 * p)                 x3             EC_ADD_BLS12_381, EC_DBL_BLS12_381
        EQ4 : s * x1 - s * x3 - y1 - y3 + (q2 * p)            y3             EC_ADD_BLS12_381, EC_DBL_BLS12_381
        EQ5 : x1 + x2 - x3 + (q1 * p)                         x3             COMPLEX_ADD_BLS12_381
        EQ6 : y1 + y2 - y3 + (q2 * p)                         y3             COMPLEX_ADD_BLS12_381
        EQ7 : x1 - x2 - x3 + (q1 * p)                         x3             COMPLEX_SUB_BLS12_381
        EQ8 : y1 - y2 - y3 + (q2 * p)                         y3             COMPLEX_SUB_BLS12_381
        EQ9 : x1 * x2 - y1 * y2 - x3 + (q1 * p)               x3             COMPLEX_MUL_BLS12_381
        EQ10: y1 * x2 + x1 * y2 - y3 + (q2 * p)               y3             COMPLEX_MUL_BLS12_381

        where p refers to the base field order of:
            · BLS12-381:  0x1A0111EA397FE69A4B1BA7B6434BACD764774B84F38512BF6730D2A0F6B0F6241EABFFFEB153FFFFB9FEFFFFFFFFAAAB
    */

    const int BLS12_381_PRIME = 0x1A0111EA397FE69A4B1BA7B6434BACD764774B84F38512BF6730D2A0F6B0F6241EABFFFEB153FFFFB9FEFFFFFFFFAAAB;

    const int CLOCKS = 24;
    // Ensure that the ARITH_EQ_384 can fit in N rows
    if (N < CLOCKS) {
        error(`N must be at least ${CLOCKS}`);
    }

    const int NUM_ARITH_EQ_384 = N / CLOCKS - 1; // The -1 is because CLOCKS is not a divisor of N
    col fixed CLK_0 = [[1, 0:(CLOCKS-1)]:NUM_ARITH_EQ_384, 0...];
    const expr LAST_CLK = (CLOCKS-1)'CLK_0;

    const int CHUNKS = CLOCKS;
    const int CBC = 2;
    const int CHUNK_BITS = 16;
    const int CHUNK_SIZE = 2**CHUNK_BITS;
    const int CHUNK_MAX = CHUNK_SIZE - 1;

    const expr CLK[CLOCKS];
    for (int i = 0; i < CLOCKS; ++i) {
        CLK[i] = (i)'CLK_0;
    }

    // TODO: carry min and max!!
    const int Q_HSC_BITS = 22; // Number of bits for high safe chunk of q
    const int Q_HSC_MAX = 2**Q_HSC_BITS-1;

    const int CARRY_BITS = 22;
    const int CARRY_MIN = -(2**CARRY_BITS - 1);
    const int CARRY_MAX = 2**CARRY_BITS;

    const int EQS = 11;     // Number of equations
    const int OPS = 6;      // Number of operations
    const int MAX_CEQS = 3; // Max concurrent equations
    const int QS = 3;       // Number of quotients

    col witness bits(CHUNK_BITS) x1;
    col witness bits(CHUNK_BITS) y1;
    col witness bits(CHUNK_BITS) x2;
    col witness bits(CHUNK_BITS) y2;
    col witness bits(CHUNK_BITS) x3;
    col witness bits(CHUNK_BITS) y3;
    col witness bits(Q_HSC_BITS) q0;
    col witness bits(Q_HSC_BITS) q1;
    col witness bits(Q_HSC_BITS) q2;
    col witness bits(Q_HSC_BITS) s;
    col witness bits(1) sel_op[OPS];


    const expr sel_arith384_mod = sel_op[0];
    const expr sel_bls12_381_curve_add = sel_op[1];
    const expr sel_bls12_381_curve_dbl = sel_op[2];
    const expr sel_bls12_381_complex_add = sel_op[3];
    const expr sel_bls12_381_complex_sub = sel_op[4];
    const expr sel_bls12_381_complex_mul = sel_op[5];

    // groups of selectors
    const expr sel_bls12_381_curve = sel_bls12_381_curve_add + sel_bls12_381_curve_dbl;
    const expr sel_bls12_381_complex = sel_bls12_381_complex_add + sel_bls12_381_complex_sub + sel_bls12_381_complex_mul;
    const expr sel_check_diff = sel_bls12_381_curve_add;
    const expr sel_check_x_lt_prime = sel_arith384_mod + sel_bls12_381_curve + sel_bls12_381_complex;
    const expr sel_check_y_lt_prime = sel_bls12_381_curve + sel_bls12_381_complex;

    const expr eq_selectors[EQS] = [sel_arith384_mod,
                                    sel_bls12_381_curve_add, sel_bls12_381_curve_dbl,
                                    sel_bls12_381_curve, sel_bls12_381_curve,
                                    sel_bls12_381_complex_add, sel_bls12_381_complex_add,
                                    sel_bls12_381_complex_sub, sel_bls12_381_complex_sub,
                                    sel_bls12_381_complex_mul, sel_bls12_381_complex_mul];

    // constraint to set (x1,y1) = (x2,y2)
    sel_bls12_381_curve_dbl * (x1 - x2) === 0;
    sel_bls12_381_curve_dbl * (y1 - y2) === 0;

    const expr chunk_cols[7] = [x1, y1, x2, y2, x3, y3, s];
    const expr qs[QS] = [q0, q1, q2];

    include "equations/arith384_mod.pil"
    include "equations/bls12_381_curve_add.pil"
    include "equations/bls12_381_curve_dbl.pil"
    include "equations/bls12_381_curve_x3.pil"
    include "equations/bls12_381_curve_y3.pil"
    include "equations/bls12_381_complex_add_x3.pil"
    include "equations/bls12_381_complex_add_y3.pil"
    include "equations/bls12_381_complex_sub_x3.pil"
    include "equations/bls12_381_complex_sub_y3.pil"
    include "equations/bls12_381_complex_mul_x3.pil"
    include "equations/bls12_381_complex_mul_y3.pil"

    col witness bits(1) sel_op_clk0[OPS];

    const expr arith384_mod_clk0 = sel_op_clk0[0];
    const expr bls12_381_curve_add_clk0 = sel_op_clk0[1];
    const expr bls12_381_curve_dbl_clk0 = sel_op_clk0[2];
    const expr bls12_381_curve_clk0 = bls12_381_curve_add_clk0 + bls12_381_curve_dbl_clk0;
    const expr bls12_381_complex_add_clk0 = sel_op_clk0[3];
    const expr bls12_381_complex_sub_clk0 = sel_op_clk0[4];
    const expr bls12_381_complex_mul_clk0 = sel_op_clk0[5];

    expr sum_sel_op = 0;
    expr sum_sel_op_clk0 = 0;
    for (int i = 0; i < OPS; ++i) {
        // binary constraint
        sel_op_clk0[i] * (1 - sel_op_clk0[i]) === 0;
        sel_op[i] * (1 - sel_op[i]) === 0;

        // selector latch
        expr sel_op_active = clock_set(start: 1, end: 20);
        sel_op_active * (sel_op[i] - 'sel_op[i]) === 0;

        // only one selector could be enabled
        sum_sel_op = sum_sel_op + sel_op[i];
        sum_sel_op_clk0 = sum_sel_op_clk0 + sel_op_clk0[i];

        // only could be one in CLK_0
        (1 - CLK_0) * sel_op_clk0[i] === 0;

        // clock of selector must be active if selector is active
        CLK_0 * (sel_op[i] - sel_op_clk0[i]) === 0;
    }

    const expr in_use = sum_sel_op;
    in_use * (1 - in_use) === 0;

    const expr in_use_clk0 = sum_sel_op_clk0;
    in_use_clk0 * (1 - in_use_clk0) === 0;

    const expr eq[MAX_CEQS][CBC];

    for (int i = 0; i < CBC; ++i) {
        eq[0][i] = expr_group_by_cbc(arith384_mod_clk0, eq_arith384_mod_chunks, i)
                 + expr_group_by_cbc(bls12_381_curve_add_clk0, eq_bls12_381_curve_add_chunks, i)
                 + expr_group_by_cbc(bls12_381_curve_dbl_clk0, eq_bls12_381_curve_dbl_chunks, i);
        eq[1][i] = expr_group_by_cbc(bls12_381_curve_clk0, eq_bls12_381_curve_x3_chunks, i)
                 + expr_group_by_cbc(bls12_381_complex_add_clk0, eq_bls12_381_complex_add_x3_chunks, i)
                 + expr_group_by_cbc(bls12_381_complex_sub_clk0, eq_bls12_381_complex_sub_x3_chunks, i)
                 + expr_group_by_cbc(bls12_381_complex_mul_clk0, eq_bls12_381_complex_mul_x3_chunks, i);
        eq[2][i] = expr_group_by_cbc(bls12_381_curve_clk0, eq_bls12_381_curve_y3_chunks, i)
                 + expr_group_by_cbc(bls12_381_complex_add_clk0, eq_bls12_381_complex_add_y3_chunks, i)
                 + expr_group_by_cbc(bls12_381_complex_sub_clk0, eq_bls12_381_complex_sub_y3_chunks, i)
                 + expr_group_by_cbc(bls12_381_complex_mul_clk0, eq_bls12_381_complex_mul_y3_chunks, i);
    }

    for (int i = 0; i < length(chunk_cols); ++i) {
        range_check(min: 0, max: CHUNK_MAX, expression: chunk_cols[i]);
        if (i < length(qs)) {
            // range_sel = 1 => [0, CHUNK_MAX]
            // range_sel = 0 => [0, Q_HSC_MAX]
            multi_range_check(min1: 0, max1: CHUNK_MAX, min2: 0, max2: Q_HSC_MAX,
                              range_sel: (1 - LAST_CLK), expression: qs[i]);
        }
    }

    // FEATURE
    // Verify that the x1,x2 are different when adding different elliptic curve points.

    // PRE
    // x1,x2,y1,y2 are alias free for elliptic curve operations

    // STRATEGY
    // If x1,x2 are different means at least in one chunk x2-x1 ≠ 0. To verify it we define
    // x_delta_chunk and his inverse, x_delta_chunk_inv. We only need in one chunk calculate
    // inverse to demostrate that x1 and x2 are different.

    const expr x_delta_chunk = x2 - x1;
    col witness bits(64) x_delta_chunk_inv;

    // x_chunk_different as binary value, only when x_delta_chunk_inv == inv(x_delta_chunk),
    // its value is 1.
    //
    // x_chunk_different: 1 ==> x1[i] != x2[i]
    //                    0 does not imply anything, x1[i] ≠ x2[i] or x1[i] == x2[i].

    const expr x_chunk_different = x_delta_chunk * x_delta_chunk_inv;
    x_chunk_different * (1 - x_chunk_different) === 0;

    // x_delta_chunk  x_delta_chunk_inv  x_chunk_different
    // -------------  -----------------  ------------------
    //        0             any          0 * any = 0            OK (1)
    //    x (≠ 0)            0           x * 0 = 0              OK (2)
    //    x (≠ 0)          inv(x)        x * inv(x) = 1         OK (3)
    //    x (≠ 0)       j ≠ {inv(x),0}   x * j = k (≠ 0,1)      FAILS (4)
    //
    // (1) and (3) are the standard cases, but (2) was introduced to avoid calculate the inverse
    // when it is not necessary. x_chunk_different is well calculed because it's 1 only when x ≠ 0
    // and  inverse it's calculated.

    // x_are_different is used to indicate that in current clock we knows that x1 and x2 are different

    col witness bits(1) x_are_different;
    x_are_different * (1 - x_are_different) === 0;

    // x_are_different in first clock that x_are_different == 1, after that x_are_different must be 0.

    x_are_different === 'x_are_different * (1 - CLK_0) + x_chunk_different;

    // 'x_are_different  CLK  x_chunk_different  x_are_different
    // ----------------  ---  -----------------  --------------
    //          0         0          0            0 OK
    //          0         0          1            1 OK
    //          0         1          0            1 OK
    //          0         1          1            1 OK
    //          1         0          0            1 OK
    //          1         0          1            2 FAILS, x_are_different ≠ 0,1 (1)
    //          1         1          0            1 OK
    //          1         1          1            1 OK

    // (1) forbidden, executor must control this situation, because expending an
    // unnecessary inverse. In last clock of cycle we check that x_are_different == 0 if
    // we need to verify this condition.

    (x_are_different - sel_check_diff) * LAST_CLK === 0;

    // FEATURE:
    //
    // Verify that the result of finite field operations are free alias, i.e. that x3,y3 ∊ [0, p-1]
    // Here, p refers to field prime number, in case of the elliptic curves is the base field.
    //
    // Limitations: Chunks are ordered from least significant to most significant.
    //
    // Strategy: Define a lt flag, and use a table to validate the transition of this flag. The table
    // has other columns as as the position (0..CHUNKS-1), the prime_id and the value of the chunk.
    // For an specific chunk of specific prime we has:
    //
    // LT_P = less than prime chunk
    // EQ_P = equal to prime chunk
    // GT_P = greater than prime chunk
    //
    // 'lt   lt   valid values
    // ---   ---  ------------
    //  0 ->  0   EQ_P, GT_P
    //  0 ->  1   LT_P
    //  1 ->  0   GT_P
    //  1 ->  1   EQ_P, LT_P
    //
    // For first clock of cycle, 'lt must be 0, we could do it we need use 'lt * (1 - CLK) on 'lt
    // column, but it increases degree. It's better use a different table for first chunk, basically
    // a table that ignores 'lt value. It's easy because we has a table by chunk and prime number.
    //
    // 'lt   lt   valid values
    // ---   ---  ------------
    //  0 ->  0   EQ_P, GT_P
    //  0 ->  1   LT_P
    //  1 ->  0   EQ_P, GT_P
    //  1 ->  1   LT_P
    //
    // This tables for chunk and prime number has size of 2 * CHUNK_SIZE rows, 2^17 for 256-bit prime
    // numbers, full table are 2^17 * 2^4 = 2^21 rows.

    // FEATURE:
    //
    // Verify that the result of modular operation, x3 = mod(x1*y1+x2, y2) are free alias, i.e.
    // that x3 ∊ [0, y2-1]
    //
    // Limitations: Chunks are ordered from least significant to most significant.
    //
    // Strategy: Simulate as substract, chunk by chunk. For readibility, we use x as result and
    // z as module (X≡x3, Z≡y2), MAX = CHUNK_SIZE - 1;
    //
    // case    X - Z           borrow
    // -----  ---------------  ------
    // X = Z  0 = ZERO           0
    // X > Z  [1, MAX] = POS     0
    // X < Z  [-1, -MAX] = NEG   1
    //
    // At end of substraction, if and only if borrow is 1, X < Z.
    // A continuation, borrow transtion table
    //
    // Xi = Zi  borrow = 'borrow
    // Xi > Zi  borrow = 0
    // Xi < Zi  borrow = 1
    //
    // Following the transtion table:
    //
    // 'borrow  borrow  valid values
    // -------  ------  ------------
    //     0  ->  0     ZERO, POS
    //     0  ->  1     NEG
    //     1  ->  0     POS
    //     1  ->  1     ZERO, NEG
    //
    // As we made for prime tables, for first clock of cycle, 'borrow must be 0, we could do it
    // using a different table for first chunk, basically a table that ignores 'borrow value.
    //
    // 'borrow  borrow  valid values
    // -------  ------  ------------
    //     0  ->  0     ZERO, POS
    //     0  ->  1     NEG
    //     1  ->  0     ZERO, POS
    //     1  ->  1     NEG
    //
    // These tables are bigger than previous prime table.
    // They has size of 2 * (2 * CHUNK_SIZE - 1) ≈ 2^18 rows, to avoid increment the degree or add
    // extra column, we replicate this table for all chunks, total size is 2^18 * 2^4 = 2^22 rows.
    // An extra column here is "pay" for each instance, but in a table is paid once.

    col witness bits(1) x3_lt;
    col witness bits(1) y3_lt;

    x3_lt * (1 - x3_lt) === 0;
    y3_lt * (1 - y3_lt) === 0;

    const expr lt_cte = 0 + clk_cte_selector(bls12_381_curve_clk0, BLS12_381_PRIME)
                          + clk_cte_selector(bls12_381_complex_add_clk0, BLS12_381_PRIME)
                          + clk_cte_selector(bls12_381_complex_sub_clk0, BLS12_381_PRIME)
                          + clk_cte_selector(bls12_381_complex_mul_clk0, BLS12_381_PRIME);
    const expr delta_x3 = x3 - y2 * sel_arith384_mod - lt_cte;
    const expr delta_y3 = y3 - lt_cte;

    lookup_assumes(ARITH_EQ_LT_TABLE_ID, [2 * 'x3_lt * (1 - CLK_0) + x3_lt, delta_x3], sel: sel_check_x_lt_prime);
    lookup_assumes(ARITH_EQ_LT_TABLE_ID, [2 * 'y3_lt * (1 - CLK_0) + y3_lt, delta_y3], sel: sel_check_y_lt_prime);

    col witness bits(64, signed) carry[MAX_CEQS][CBC];
    for (int i = 0; i < MAX_CEQS; ++i) {
        // Initial carry must be 0
        carry[i][0] * CLK_0 === 0;
        for (int j = 0; j < CBC; ++j) {
            range_check(min: CARRY_MIN, max: CARRY_MAX, expression: carry[i][j]);
            if (j == CBC - 1) {
                // in the last clock the equation should be satisfied and carry must be 0
                eq[i][j] + carry[i][j] === (1 - LAST_CLK) * carry[i][0]' * CHUNK_SIZE;
            } else {
                eq[i][j] + carry[i][j] === carry[i][j+1] * CHUNK_SIZE;
            }
        }
    }

    // There are some values that no change during all cycle, normally we define a constraint
    // like latch to guarantee that these values are the same in all clocks. In this case, we
    // define that for some of these type of values, only "use" one clock inside the cycle and
    // use only one column for different values.

    // In this case, step_addr is the column, in first clock of cycle, mem_step is step_addr, but
    // in clock 1 is 'step_addr, in clock 2 is 2'step_addr, and so on. In case of addr, in clock 0
    // is step_addr', in clock 1 is step_addr, in clock 2 is 'step_addr, and so on.

    // With this technique we could save some columns, but it isn't free, because if we would build
    // a expression to read a value for any clock, we need to define a sequence of sums with multiplication
    // of clock selector and step_addr. This expression has degree 2. In some situations it could be
    // useful to save columns.

    col witness bits(40) step_addr;

    const int MAIN_STEP = 0;

    // 6 reads    384 bits  = 4 * 6 = 24 reads 64 bits
    // 2 writes   384 bits  = 2 * 6 = 12 writes 64 bits
    // 6 indirect  64 bits  = 6 * 1 =  6 reads 64 bits
    //                     TOTAL    = 42 memory accesses

    //                        READ           WRITE      INDIRECT
    // arith384_mod           x1,y1,x2,y2    x3         x1,y1,x2,y2,x3
    // bls12_381_curve_add    x1,y1,x2,y2    x3,y3      x1,y1,x2,y2
    // bls12_381_curve_dbl    x1,y2          x3,y3      x1,y1
    // bls12_381_complex_add  x1,y1,x2,y2    x3,y3      x1,y1,x2,y2
    // bls12_381_complex_sub  x1,y1,x2,y2    x3,y3      x1,y1,x2,y2
    // bls12_381_complex_mul  x1,y1,x2,y2    x3,y3      x1,y1,x2,y2

    const int ADDR_OP = MAIN_STEP + 1;
    const int ADDR_X1 = ADDR_OP + 1;
    const int ADDR_Y1 = ADDR_X1 + 1;
    const int ADDR_X2 = ADDR_X1 + 2;
    const int ADDR_Y2 = ADDR_X1 + 3;
    const int ADDR_X3 = ADDR_X1 + 4;
    const int ADDR_Y3 = ADDR_X1 + 5;

    const int ADDR_IND_0 = ADDR_Y3 + 1;
    const int ADDR_IND_1 = ADDR_IND_0 + 1;
    const int ADDR_IND_2 = ADDR_IND_0 + 2;
    const int ADDR_IND_3 = ADDR_IND_0 + 3;
    const int ADDR_IND_4 = ADDR_IND_0 + 4;
    const int ADDR_IND_5 = ADDR_IND_0 + 5;

    // MEMORY ACCESS MAP
    //
    //  0  STEP     | R | ADDR_X1       x1[0..3]    1         | ADDR_Y1       y1[0..3]    1
    //  1  STEP     | R | ADDR_X1 + 8   x1[4..7]    1         | ADDR_Y1 + 8   y1[4..7]    1
    //  2  STEP     | R | ADDR_X1 + 16  x1[8..11]   1         | ADDR_Y1 + 16  y1[8..11]   1
    //  3  STEP     | R | ADDR_X1 + 24  x1[12..15]  1         | ADDR_Y1 + 24  y1[12..15]  1
    //  4  STEP     | R | ADDR_X1 + 32  x1[16..19]  1         | ADDR_Y1 + 32  y1[16..19]  1
    //  5  STEP     | R | ADDR_X1 + 40  x1[20..23]  1         | ADDR_Y1 + 40  y1[20..23]  1
    //  6  STEP     | R | ADDR_X2       x2[0..3]    1         | ADDR_Y2       y2[0..3]    use_y2
    //  7  STEP     | R | ADDR_X2 + 8   x2[4..7]    1         | ADDR_Y2 + 8   y2[4..7]    use_y2
    //  8  STEP     | R | ADDR_X2 + 16  x2[8..11]   1         | ADDR_Y2 + 16  y2[8..11]   use_y2
    //  9  STEP     | R | ADDR_X2 + 24  x2[12..15]  1         | ADDR_Y2 + 24  y2[12..15]  use_y2
    // 10  STEP     | R | ADDR_X2 + 32  x2[16..19]  1         | ADDR_Y2 + 32  y2[16..19]  use_y2
    // 11  STEP     | R | ADDR_X2 + 40  x2[20..23]  1         | ADDR_Y2 + 40  y2[20..23]  use_y2
    // 12  STEP + 1 | W | ADDR_X3       x3[0..3]    1         | ADDR_Y3       y3[0..3]    use_y3
    // 13  STEP + 1 | W | ADDR_X3 + 8   x3[4..7]    1         | ADDR_Y3 + 8   y3[4..7]    use_y3
    // 14  STEP + 1 | W | ADDR_X3 + 16  x3[8..11]   1         | ADDR_Y3 + 16  y3[8..11]   use_y3
    // 15  STEP + 1 | W | ADDR_X3 + 24  x3[12..15]  1         | ADDR_Y3 + 24  y3[12..15]  use_y3
    // 16  STEP + 1 | W | ADDR_X3 + 32  x3[16..19]  1         | ADDR_Y3 + 32  y3[16..19]  use_y3
    // 17  STEP + 1 | W | ADDR_X3 + 40  x3[20..23]  1         | ADDR_Y3 + 40  y3[20..23]  use_y3
    // 18  STEP     | R | ADDR_OP + 0   ADDR_IND_0  use_ind_0 | ADDR_OP + 8   ADDR_IND_1  use_ind_1
    // 19  STEP     | R | ADDR_OP + 16  ADDR_IND_2  use_ind_2 | ADDR_OP + 24  ADDR_IND_3  use_ind_3
    // 20  STEP     | R | ADDR_OP + 32  ADDR_IND_4  use_ind_4 | ADDR_OP + 40  ADDR_IND_5  use_ind_5
    // 21  ---       ----------     -------                 ----------      ---------
    // 22  ---       ----------     -------                 ----------      ---------
    // 23  ---       ----------     -------                 ----------      ---------

    const expr use_x2 = sel_arith384_mod + sel_bls12_381_curve_add + sel_bls12_381_complex;
    const expr use_y2 = sel_arith384_mod + sel_bls12_381_curve_add + sel_bls12_381_complex;
    const expr use_y3 = sel_bls12_381_curve_add + sel_bls12_381_curve_dbl + sel_bls12_381_complex;

    // [arith384_mod]        ADDR_X1 === ADDR_IND_0
    //
    // [bls12_381_curve_add] ADDR_X1 === ADDR_IND_0,
    //                       ADDR_Y1 === ADDR_IND_0 + 48
    //                       ADDR_X3 === ADDR_X1, ADDR_Y3 === ADDR_Y1
    //
    // [bls12_381_curve_dbl] ADDR_X1 === ADDR_OP,
    //                       ADDR_Y1 === ADDR_OP + 48
    //                       ADDR_X3 === ADDR_X1, ADDR_Y3 === ADDR_Y1
    //
    // [bls12_381_complex]   ADDR_X1 === ADDR_IND_0
    //                       ADDR_Y1 === ADDR_IND_0 + 48
    //                       ADDR_X3 === ADDR_X1, ADDR_Y3 === ADDR_Y1

    const expr use_ind_0 = sel_arith384_mod + sel_bls12_381_curve_add + sel_bls12_381_complex;

    (sel_arith384_mod + sel_bls12_381_curve_add + sel_bls12_381_complex) * clock_eq(step_addr, ADDR_X1, ADDR_IND_0) === 0;
    (sel_bls12_381_curve_add + sel_bls12_381_complex) * clock_eq(step_addr, ADDR_Y1, ADDR_IND_0, 48) === 0;

    sel_bls12_381_curve_dbl * clock_eq(step_addr, ADDR_X1, ADDR_OP) === 0;
    sel_bls12_381_curve_dbl * clock_eq(step_addr, ADDR_Y1, ADDR_OP, 48) === 0;

    (sel_bls12_381_curve + sel_bls12_381_complex) * clock_eq(step_addr, ADDR_X1, ADDR_X3) === 0;
    (sel_bls12_381_curve + sel_bls12_381_complex) * clock_eq(step_addr, ADDR_Y1, ADDR_Y3) === 0;

    // [arith384_mod]        ADDR_Y1 === ADDR_IND_1
    //
    // [bls12_381_curve_add] ADDR_X2 === ADDR_IND_1
    //                       ADDR_Y2 === ADDR_IND_1 + 48
    //
    // [bls12_381_complex]   ADDR_X2 === ADDR_IND_1
    //                       ADDR_Y2 === ADDR_IND_1 + 48

    const expr use_ind_1 = sel_arith384_mod + sel_bls12_381_curve_add + sel_bls12_381_complex;

    sel_arith384_mod * clock_eq(step_addr, ADDR_Y1, ADDR_IND_1) === 0;

    (sel_bls12_381_curve_add + sel_bls12_381_complex) * clock_eq(step_addr, ADDR_X2, ADDR_IND_1) === 0;
    (sel_bls12_381_curve_add + sel_bls12_381_complex) * clock_eq(step_addr, ADDR_Y2, ADDR_IND_1, 48) === 0;

    // [arith384_mod]  ADDR_X2 === ADDR_IND_2

    const expr use_ind_2 = sel_arith384_mod;

    sel_arith384_mod * clock_eq(step_addr, ADDR_X2, ADDR_IND_2) === 0;

    // [arith384_mod]  ADDR_Y2 === ADDR_IND_3

    const expr use_ind_3 = sel_arith384_mod;

    sel_arith384_mod * clock_eq(step_addr, ADDR_Y2, ADDR_IND_3) === 0;

    // [arith384_mod]  ADDR_X3 === ADDR_IND_4

    const expr use_ind_4 = sel_arith384_mod;

    sel_arith384_mod * clock_eq(step_addr, ADDR_X3, ADDR_IND_4) === 0;

    const expr use_ind_5 = 0;

    const expr mem_addr[2];
    mem_addr[0] = clock_map(step_addr, ADDR_X1, start: 0,  end: 5,  delta: 8) +
                  clock_map(step_addr, ADDR_X2, start: 6,  end: 11, delta: 8) +
                  clock_map(step_addr, ADDR_X3, start: 12, end: 17, delta: 8) +
                  clock_map(step_addr, ADDR_OP, start: 18, end: 20, delta: 16);

    mem_addr[1] = clock_map(step_addr, ADDR_Y1, start: 0,  end: 5,  delta: 8) +
                  clock_map(step_addr, ADDR_Y2, start: 6,  end: 11, delta: 8) +
                  clock_map(step_addr, ADDR_Y3, start: 12, end: 17, delta: 8) +
                  clock_map(step_addr, ADDR_OP, start: 18, end: 20, delta: 16, offset: 8);

    const expr main_step = clock_map(step_addr, MAIN_STEP, start: 0, end: 20);

    const expr mem_is_write = CLK[12] + CLK[13] + CLK[14] + CLK[15] + CLK[16] + CLK[17];

    const expr mem_value[2][2];

    mem_value[0][0] =   map_chunks_to_m32(x1, start: 0,  end: 5) +
                        map_chunks_to_m32(x2, start: 6,  end: 11) +
                        map_chunks_to_m32(x3, start: 12, end: 17) +
                        clock_map(step_addr, ADDR_IND_0, 18) +
                        clock_map(step_addr, ADDR_IND_2, 19) +
                        clock_map(step_addr, ADDR_IND_4, 20);

    mem_value[0][1] =   map_chunks_to_m32(x1, index: 2, start: 0,  end: 5) +
                        map_chunks_to_m32(x2, index: 2, start: 6,  end: 11) +
                        map_chunks_to_m32(x3, index: 2, start: 12, end: 17);
                        // high bits of indirect addresses are 0.

    mem_value[1][0] =   map_chunks_to_m32(y1, start: 0,  end: 5) +
                        map_chunks_to_m32(y2, start: 6,  end: 11) +
                        map_chunks_to_m32(y3, start: 12, end: 17) +
                        clock_map(step_addr, ADDR_IND_1, 18) +
                        clock_map(step_addr, ADDR_IND_3, 19) +
                        clock_map(step_addr, ADDR_IND_5, 20);

    mem_value[1][1] =   map_chunks_to_m32(y1, index: 2, start: 0,  end: 5) +
                        map_chunks_to_m32(y2, index: 2, start: 6,  end: 11) +
                        map_chunks_to_m32(y3, index: 2, start: 12, end: 17);
                        // high bits of indirect addresses are 0.

    const expr mem_sel[2];

    mem_sel[0] = clock_set(in_use, start: 0,  end: 5) +
                 clock_set(use_x2, start: 6,  end: 11) +
                 clock_set(in_use, start: 12, end: 17) +
                 clock_set(use_ind_0, 18) +
                 clock_set(use_ind_2, 19) +
                 clock_set(use_ind_4, 20);

    mem_sel[1] = clock_set(in_use, start: 0,  end: 5) +
                 clock_set(use_y2, start: 6,  end: 11) +
                 clock_set(use_y3, start: 12, end: 17) +
                 clock_set(use_ind_1, 18) +
                 clock_set(use_ind_3, 19) +
                 clock_set(use_ind_5, 20);


    precompiled_mem_op(
            is_write: mem_is_write,
            sel: mem_sel[0],
            main_step: main_step,
            addr: mem_addr[0],
            value: mem_value[0]
    );

    precompiled_mem_op(
            is_write: mem_is_write,
            sel: mem_sel[1],
            main_step: main_step,
            addr: mem_addr[1],
            value: mem_value[1]
    );

    const expr bus_op = sel_arith384_mod * ARITH_384_MOD_OP +
                        sel_bls12_381_curve_add * EC_ADD_BLS12_381_OP +
                        sel_bls12_381_curve_dbl * EC_DBL_BLS12_381_OP +
                        sel_bls12_381_complex_add * COMPLEX_ADD_BLS12_381_OP +
                        sel_bls12_381_complex_sub * COMPLEX_SUB_BLS12_381_OP +
                        sel_bls12_381_complex_mul * COMPLEX_MUL_BLS12_381_OP;

    lookup_proves(operation_bus_id, [bus_op, step_addr'(MAIN_STEP), 0, step_addr'(ADDR_OP), 0, 0, 0, 0], mul: in_use_clk0);

    // selclk0 is the clock 0 for dedicated to one operation
    function expr_group_by_cbc(const expr selclk0, const expr chunks[], const int index): const expr {
        expr res = 0;
        int iclock = 0;
        for (int i = 0; i < length(chunks); ++i) {
            if (i % air.CBC == index) {
                res += (iclock)'selclk0 * chunks[i];
                ++iclock;
            }
        }
        return res;
    }

    function map_chunks_to_m32(const expr cl, int index = 0, int start = 0, int end = 0, int delta_index = 4): const expr {
        expr res = 0;
        for (int iclock = start; iclock <= end; ++iclock) {
            res += air.CLK[iclock]*(index_on_clock(cl, index, iclock) + air.CHUNK_SIZE * index_on_clock(cl, index + 1, iclock));
            index += delta_index;
        }
        return res;
    }

    function chunk_to_m32_on_clock(const expr cl, int index, int current_clock): const expr {
        return air.CLK[cl]*(index_on_clock(cl, index, current_clock) + index_on_clock(cl, index + 1, current_clock));
    }

    function index_on_clock(const expr cl, int index, int current_clock): const expr {
        return cl'(index - current_clock);
    }

    function clock_map(const expr mvcol, int pos, int start = 0, int end = -1, int offset = 0, int delta = 0): const expr {
        expr res = 0;
        if (end == -1) {
            end = start;
        }
        for (int index = start; index <= end; ++index) {
            int iclock = (pos - index ) % air.CLOCKS;
            if (offset != 0) {
                res += air.CLK[index] * (mvcol'(iclock) + offset);
            } else {
                res += air.CLK[index] * mvcol'(iclock);
            }
            offset += delta;
        }
        return res;
    }

    function clock_set(const expr cl = 1, int start = 0, int end = -1, int offset = 0, int delta = 0): const expr {
        expr res = 0;
        if (end == -1) {
            end = start;
        }
        for (int index = start; index <= end; ++index) {
            if (offset != 0) {
                res += air.CLK[index] * (cl + offset);
            } else {
                res += air.CLK[index] * cl;
            }
            offset += delta;
        }
        return res;
    }

    function clock_eq(const expr mvcol, int pos1, int pos2, int pos2_offset = 0): const expr {
        return air.CLK_0 * (mvcol'(pos1) - mvcol'(pos2) - pos2_offset);
    }

    function clk_cte_selector(const expr selclk0, int value, int chunk_bits = 16, int chunks = 24): expr {
        const int mask = (1 << chunk_bits) - 1;
        expr result = 0;
        for (int i_chunk = 0; i_chunk < chunks; ++i_chunk) {
            result += (i_chunk)'selclk0 * (value & mask);
            value = value >> chunk_bits;
        }
        return result;
    }
}