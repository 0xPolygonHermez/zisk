require "std_constants.pil"
require "std_lookup.pil"
require "std_connection.pil"

// Precompile in charge of performing the Keccak-f[1600] permutation.
// For reference: https://keccak.team/files/Keccak-reference-3.0.pdf

const int KECCAKF_OP = 0xF1;

airtemplate Keccakf(const int N, const int chunks, const int bits, const int RC, const int RB, 
                    const int mem_bits_in_parallel, const int operation_bus_id = OPERATION_BUS_ID) {
    
    #pragma extern_fixed_file "../src/keccakf_fixed.bin"
    
    // Keccak-f circuit size
    const int CIRCUIT_SIZE = 93846;
    println(`The size of the Keccak-f circuit is ${CIRCUIT_SIZE}`);

    // Ensure that the keccak-f circuit fits
    if (N < CIRCUIT_SIZE) {
        error(`N must be at least ${CIRCUIT_SIZE} to fit the Keccak-f circuit, but received N=${N}`);
    }

    // Ensure that the keccak-f input fit in the prime field
    const int CHUNKS_BITS = chunks * bits;
    if (CHUNKS_BITS > log2(PRIME)) {
        error(`The number of bits ${CHUNKS_BITS} is too large for the prime ${PRIME}`);
    }

    // Compute some stats
    const int NUM_KECCAKF_PER_CIRCUIT = CHUNKS_BITS;
    const int NUM_CIRCUITS = (N - 1) / CIRCUIT_SIZE;
    const int NUM_KECCAKF = NUM_CIRCUITS * NUM_KECCAKF_PER_CIRCUIT;
    const int NON_USABLE_ROWS = (N - 1) % CIRCUIT_SIZE;
    println(`The number of Keccak-f per circuit is ${NUM_KECCAKF_PER_CIRCUIT}`);
    println(`The number of circuits that fit wih N=2^${log2(N)} is ${NUM_CIRCUITS} and the total number of Keccak-f is ${NUM_KECCAKF}`);
    println(`The number of non-usable (padding) rows is ${NON_USABLE_ROWS}`);

    const int P2_BITS = 2**bits;
    const int P2_CHUNKS_BITS = 2**(bits * chunks);
    const int MASK_CHUNKS_BITS = P2_CHUNKS_BITS - 1;

    col fixed L1 = [1,0...];

    // Column specifying the circuit gates    
    col fixed GATE_OP;

    // Columns specifying the circuit connections
    col fixed CONN_A;
    col fixed CONN_B;
    col fixed CONN_C;
    col fixed CONN_D;

    col witness bits(bits) free_in_a[chunks];
    col witness bits(bits) free_in_b[chunks];
    col witness bits(bits) free_in_c[chunks];
    col witness bits(bits) free_in_d[chunks];

    // --> Circuit gates constraints
    for (int i = 0; i < chunks; i++) {
        lookup_assumes(KECCAKF_TABLE_ID, [GATE_OP, free_in_a[i], free_in_b[i], free_in_c[i], free_in_d[i]]);
    }

    // --> Circuit connections constraints
    // Start by reconstructing the input from the chunks
    expr a = 0;
    expr b = 0;
    expr c = 0;
    expr d = 0;
    for (int i = 0; i < chunks; i++) {
        const int shift = P2_BITS**i;
        a += free_in_a[i] * shift;
        b += free_in_b[i] * shift;
        c += free_in_c[i] * shift;
        d += free_in_d[i] * shift;
    }

    // Enforce constants 0b00..000 and 0b11..111 at the first row for direct access
    // This reduces the circuit connections
    L1 * a === 0;
    L1 * (b - MASK_CHUNKS_BITS) === 0;

    const int CIRCUIT_ID = 0x190;
    connection(opid: CIRCUIT_ID, cols: [a, b, c, d], CONN: [CONN_A, CONN_B, CONN_C, CONN_D]);

    // --> Constraints to read inputs from memory and write outputs to memory

    /*
        We should take care of how we handle the memory access.

        Circuit inputs are represented as a vector of bits, where each bit is
        from a different Keccakf invocation. However, the Mem treats one
        Keccakf invocation at a time, so we need to relate these two
        representations. For that, we will use a bit decomposition and prove
        that the bit decomposition is consistent with the Keccakf circuit.

        Example with NUM_KECCAKF_PER_CIRCUIT = 63
           ROW       BIT  BIT[0]    VAL[0]        BIT     BIT[1]    VAL[1]        BLOCK   TYPE  
        | 1       | 1    |  X   |   0bX        |   2     |  X   |   0bX        |    1    | IN  | 
        | 2       | 1    |  Y   |   0bYX       |   2     |  Y   |   0bYX       |    1    | IN  |       
        | ...     | ...  | ...  |              |   ...   | ...  |              |   ...   |     | 
        | 63      | 1    |  Z   |   0bZ...YX   |   2     |  Z   |   0bZ...YX   |    1    | IN  |
        ----------------------------------------------------------------------------------------
        | 64      | 3    |  X   |   0bX        |   4     |  X   |   0bX        |    2    | IN  | 
        | 65      | 3    |  Y   |   0bYX       |   4     |  Y   |   0bYX       |    2    | IN  |       
        | ...     | ...  | ...  |              |   ...   | ...  |              |   ...   |     | 
        | 126     | 3    |  Z   |   0bZ...YX   |   4     |  Z   |   0bZ...YX   |    2    | IN  | 
        ----------------------------------------------------------------------------------------
        |......................................................................................|
        ----------------------------------------------------------------------------------------
        | 50.337  | 1599 |  X   |   0bX        |   1600  |  X   |   0bX        |   800   | IN  | 
        | 50.338  | 1599 |  Y   |   0bYX       |   1600  |  Y   |   0bYX       |   800   | IN  |       
        | ...     | ...  | ...  |              |   ...   | ...  |              |   ...   |     |  
        | 50.400  | 1599 |  Z   |   0bZ...YX   |   1600  |  Z   |   0bZ...YX   |   800   | IN  |
        ----------------------------------------------------------------------------------------
        | 50.401  | 1    |  X   |   0bX        |   2     |  X   |   0bX        |    1    | OUT | 
        | 50.402  | 1    |  Y   |   0bYX       |   2     |  Y   |   0bYX       |    1    | OUT |       
        | ...     | ...  | ...  |              |   ...   | ...  |              |   ...   |     |  
        | 50.463  | 1    |  Z   |   0bZ...YX   |   2     |  Z   |   0bZ...YX   |    1    | OUT | 
        ----------------------------------------------------------------------------------------
        |......................................................................................|
        ----------------------------------------------------------------------------------------
        | 100.737 | 1599 |  X   |   0bX        |   1600  |  X   |   0bX        |   800   | OUT | 
        | 100.738 | 1599 |  Y   |   0bYX       |   1600  |  Y   |   0bYX       |   800   | OUT |       
        | ...     | ...  | ...  |              |   ...   | ...  |              |   ...   |     |  
        | 100.800 | 1599 |  Z   |   0bZ...YX   |   1600  |  Z   |   0bZ...YX   |   800   | OUT |      
        ----------------------------------------------------------------------------------------          
    */

    col witness bits(1) bit[mem_bits_in_parallel];
    col witness bits(64) val[mem_bits_in_parallel];

    // Ensure that bit[i] is a bit
    for (int i = 0; i < mem_bits_in_parallel; i++) {
        bit[i] * (1 - bit[i]) === 0; // (1)
    }

    // Parameters bits
    const int INPUT_BITS = 1600;
    const int OUTPUT_BITS = INPUT_BITS;
    const int INPUT_OUTPUT_BITS = INPUT_BITS + OUTPUT_BITS;
    if ((INPUT_BITS % mem_bits_in_parallel != 0) || (OUTPUT_BITS % mem_bits_in_parallel != 0)) {
        error(`mem_bits_in_parallel=${mem_bits_in_parallel} must be a factor of INPUT_BITS=${INPUT_BITS} and OUTPUT_BITS=${OUTPUT_BITS}`);
    }

    // Bitwise decomposition
    const int INPUT_SIZE = INPUT_BITS * NUM_KECCAKF_PER_CIRCUIT / mem_bits_in_parallel;
    const int OUTPUT_SIZE = OUTPUT_BITS * NUM_KECCAKF_PER_CIRCUIT / mem_bits_in_parallel;
    const int INPUT_OUTPUT_SIZE = INPUT_SIZE + OUTPUT_SIZE;
    if (INPUT_OUTPUT_SIZE > CIRCUIT_SIZE) {
        error(`The Keccak-f circuit size ${CIRCUIT_SIZE} is too small to fit the bit decomposition of the inputs and outputs: 
               INPUT_OUTPUT_SIZE=${INPUT_OUTPUT_SIZE} (INPUT_BITS=${INPUT_BITS}, OUTPUT_BITS=${OUTPUT_BITS}, NUM_KECCAKF_PER_CIRCUIT=${NUM_KECCAKF_PER_CIRCUIT}, mem_bits_in_parallel=${mem_bits_in_parallel})`);  
    }
    println(`The bit decomposition of the inputs and outputs occupies ${INPUT_OUTPUT_SIZE} rows (INPUT_BITS=${INPUT_BITS}, OUTPUT_BITS=${OUTPUT_BITS}, NUM_KECCAKF_PER_CIRCUIT=${NUM_KECCAKF_PER_CIRCUIT}, mem_bits_in_parallel=${mem_bits_in_parallel})`);

    // Instructions to add a latch:
    //  - When there are circuits.
    //  - Every NUM_KECCAKF_PER_CIRCUIT clocks.
    //  - When there are still bits to process.
    col fixed LATCH_NUM_KECCAKF  = [0,[0,[0:(NUM_KECCAKF_PER_CIRCUIT-1),1]:(INPUT_OUTPUT_BITS / mem_bits_in_parallel),
                                       0:(CIRCUIT_SIZE - (INPUT_OUTPUT_SIZE+1))]:NUM_CIRCUITS,
                                    0...];

    // Instructions to add the factor:
    //  - When there are circuits.
    //  - Every clock, for the clock in the circuit, we add the factor 1 << clk.
    //  - When there are still bits to process (i.e., we repeat the previous instruction KECCAKF_SIZE_BITS times).
    col fixed FACTOR_NUM_KECCAKF;
    if (NUM_KECCAKF_PER_CIRCUIT != 1) {
        FACTOR_NUM_KECCAKF = [0,[[1,2..*..2**(NUM_KECCAKF_PER_CIRCUIT-1)]:(INPUT_OUTPUT_BITS / mem_bits_in_parallel), 
                                  0:(CIRCUIT_SIZE - INPUT_OUTPUT_SIZE)]:NUM_CIRCUITS,
                              0...];
    } else {
        FACTOR_NUM_KECCAKF = [0, [1:(INPUT_OUTPUT_BITS / mem_bits_in_parallel), 
                                  0:(CIRCUIT_SIZE - INPUT_OUTPUT_SIZE)]:NUM_CIRCUITS, 
                              0...];
    }

    // val[i] accumulates the bits
    for (int i = 0; i < mem_bits_in_parallel; i++) {
        val[i]' === val[i] * (1 - LATCH_NUM_KECCAKF) + bit[i] * FACTOR_NUM_KECCAKF; // (2)
    }
    // In each j-th latch, val[i] has accumulated the j-th bit of all the keccakf's 

    // In each j-th latch, val[i] has to coincide with the a-th gate entry,
    // whose positions where reserved for the inputs and outputs of the Keccakf circuit
    for (int i = 0; i < mem_bits_in_parallel; i++) {
        LATCH_NUM_KECCAKF * (val[i] - a'(i)) === 0; // (3)
    }

    // (1),(2) and (3) prove that the bit decomposition is correct

    // A single col is sufficient for storing the step and the address
    col witness bits(40) step_addr;

    // Number of memory accesses needed to handle the bits
    const int MEM_BITS = RB * RC;
    const int IN_BLOCKS = INPUT_BITS / MEM_BITS;
    const int OUT_BLOCKS = OUTPUT_BITS / MEM_BITS;
    const int IN_OUT_BLOCKS = IN_BLOCKS + OUT_BLOCKS;
    const int MEM_SIZE = MEM_BITS * NUM_KECCAKF_PER_CIRCUIT / mem_bits_in_parallel;
    if ((RB % mem_bits_in_parallel != 0) || (MEM_BITS % mem_bits_in_parallel != 0)) {
        error(`mem_bits_in_parallel=${mem_bits_in_parallel} must be a factor of RB=${RB} and MEM_BITS=${MEM_BITS}`);
    }

    /*
        MEMORY ACCESS MAP (with NUM_KECCAKF_PER_CIRCUIT = 63 and mem_bits_in_parallel = 4):
        =================================================================
        1      STEP_MAIN_1      | R |  ADDR_STATE_1        |  state_1[0]
        2      STEP_MAIN_2      | R |  ADDR_STATE_2        |  state_2[0]
        ...    ...              |   |       ...            |    ...       
        63     STEP_MAIN_63     | R |  ADDR_STATE_63       |  state_63[0]
        ----------------------------------------------------------------- 
        ...    ...              |   |       ...            |    ...    
        ----------------------------------------------------------------- 
        1009   STEP_MAIN_1      | R |  ADDR_STATE_1 + 8    |  state_1[1]
        1010   STEP_MAIN_2      | R |  ADDR_STATE_2 + 8    |  state_2[1]
        ...    ...              |   |       ...            |    ...   
        1071   STEP_MAIN_63     | R |  ADDR_STATE_63 + 8   |  state_63[1] 
        ----------------------------------------------------------------- 
        ...    ...              |   |       ...            |    ...   
        -----------------------------------------------------------------
        24.193 STEP_MAIN_1      | R |  ADDR_STATE_1 + 192  |  state_1[24] 
        24.194 STEP_MAIN_2      | R |  ADDR_STATE_2 + 192  |  state_2[24]  
        ...    ...              |   |       ...            |    ...   
        24.255 STEP_MAIN_63     | R |  ADDR_STATE_63 + 192 |  state_63[24]  
        -----------------------------------------------------------------  
        ...    ...              |   |       ...            |    ...   
        -----------------------------------------------------------------
        25.201 STEP_MAIN_1 + 1  | W |  ADDR_STATE_1        |  state_1[0]
        25.202 STEP_MAIN_2 + 1  | W |  ADDR_STATE_2        |  state_2[0]
        ...    ...              |   |       ...            |    ...       
        25.263 STEP_MAIN_63 + 1 | W |  ADDR_STATE_63       |  state_63[0]
        ----------------------------------------------------------------- 
        ...    ...              |   |       ...            |    ...   
        -----------------------------------------------------------------
        26.209 STEP_MAIN_1 + 1  | W |  ADDR_STATE_1 + 8    |  state_1[1]
        26.210 STEP_MAIN_2 + 1  | W |  ADDR_STATE_2 + 8    |  state_2[1]
        ...    ...              |   |       ...            |    ...   
        26.271 STEP_MAIN_63 + 1 | W |  ADDR_STATE_63 + 8   |  state_63[1] 
        ----------------------------------------------------------------- 
        ...    ...              |   |       ...            |    ...   
        -----------------------------------------------------------------
        49.393 STEP_MAIN_1 + 1  | W |  ADDR_STATE_1 + 192  |  state_1[24] 
        49.394 STEP_MAIN_2 + 1  | W |  ADDR_STATE_2 + 192  |  state_2[24]  
        ...    ...              |   |       ...            |    ...   
        49.455 STEP_MAIN_63 + 1 | W |  ADDR_STATE_63 + 192 |  state_63[24]   
        -----------------------------------------------------------------
        ...    ...              |   |       ...            |    ...   
        =================================================================
    */

    col fixed CLK_0 = [0, [1:NUM_KECCAKF_PER_CIRCUIT, 0:(CIRCUIT_SIZE-NUM_KECCAKF_PER_CIRCUIT)]:NUM_CIRCUITS, 0...];
    expr CLK[CIRCUIT_SIZE];
    for (int i = 0; i < CIRCUIT_SIZE; i++) {
        CLK[i] = (i)'CLK_0;
    }

    const int STEP_MAIN = 0;
    const int ADDR_STATE = STEP_MAIN + MEM_SIZE;

    // keccakf_val[i] accumulates RB bits of the j-th Keccakf invocation each clock
    expr keccakf_val[RC];
    // Initialize the keccakf_val
    for (int i = 0; i < RC; i++) {
        keccakf_val[i] = 0;
    }
    // Update the keccakf_val: the Keccakf works over words of 64 bits
    // so it aligns perfectly with our memory of 64 bits
    const int RB_OFFSET = RB / mem_bits_in_parallel;
    for (int i = 0; i < RB; i++) {
        const int keccakf_val_bit = i % mem_bits_in_parallel;
        const int keccakf_val_idx = i / mem_bits_in_parallel;
        for (int j = 0; j < RC; j++) {
            keccakf_val[j] += bit[keccakf_val_bit]'(NUM_KECCAKF_PER_CIRCUIT*(RB_OFFSET * j + keccakf_val_idx)) * 2**i;
        }
    }
    // Example with NUM_KECCAKF_PER_CIRCUIT = 63 and mem_bits_in_parallel = 2:
    // keccakf_val[0] = bit[0]        + bit[1]·2        + bit[0]'(63)·2²   + bit[1]'(63)·2³   + ... + bit[0]'(945)·2³⁰  + bit[1]'(945)·2³¹
    // keccakf_val[1] = bit[0]'(1008) + bit[1]'(1008)·2 + bit[0]'(1071)·2² + bit[1]'(1071)·2³ + ... + bit[0]'(1890)·2³⁰ + bit[1]'(1890)·2³¹

    expr mem_value[RC];
    for (int i = 0; i < RC; i++) {
        mem_value[i] = clock_set(keccakf_val[i], start: 0, end: IN_OUT_BLOCKS, factor: MEM_SIZE);
    }

    const expr main_step = clock_map(step_addr, pos: STEP_MAIN, start: 0, end: IN_OUT_BLOCKS, factor: MEM_SIZE);

    const expr mem_addr = clock_map(step_addr, pos: ADDR_STATE, start: 0,         end: IN_BLOCKS,     delta: 8, factor: MEM_SIZE) +
                          clock_map(step_addr, pos: ADDR_STATE, start: IN_BLOCKS, end: IN_OUT_BLOCKS, delta: 8, factor: MEM_SIZE);

    col witness bits(1) in_use_clk_0;
    in_use_clk_0 * (1 - in_use_clk_0) === 0;
    (1 - CLK_0) * in_use_clk_0 === 0; // it can only be active when CLK_0 is active

    col witness bits(1) in_use;
    in_use * (1 - in_use) === 0;

    // if in_use is activated, it must be activated until there are not more input/output blocks
    (1 - (CLK_0 + (IN_OUT_BLOCKS*MEM_SIZE)'CLK_0)) * (in_use - (MEM_SIZE)'in_use) === 0;

    // if in_use_clk_0 is active then so is in_use
    CLK_0 * (in_use - in_use_clk_0) === 0;

    const expr mem_sel = clock_set(in_use, start: 0, end: IN_OUT_BLOCKS, factor: MEM_SIZE);

    const expr mem_is_write = clock_set(start: 0, end: OUT_BLOCKS, offset: IN_BLOCKS, factor: MEM_SIZE);

    precompiled_mem_op(
            is_write: mem_is_write,
            sel: mem_sel,
            main_step: main_step,
            addr: mem_addr,
            value: mem_value
    );

    // --> Constraints to make sure that this coprocessor is called from the main processor
    lookup_proves(operation_bus_id, [KECCAKF_OP, step_addr'(STEP_MAIN), 0, step_addr'(ADDR_STATE), 0, 0, 0, 0], mul: in_use_clk_0);

    function clock_map(const expr cl, int pos, int start = 0, int end = -1, int delta = 0, int factor = 1): const expr {
        expr res = 0;
        if (end == -1) {
            end = start + 1;
        }
        int offset = 0;
        for (int index = start; index < end; index++) {
            int _index = index*factor;
            int iclock = (pos - _index) % air.CIRCUIT_SIZE;
            if (offset != 0) {
                res += air.CLK[_index] * (cl'(iclock) + offset);
            } else {
                res += air.CLK[_index] * cl'(iclock);
            }
            offset += delta;
        }
        return res;
    }

    function clock_set(const expr cl = 1, int start = 0, int end = -1, int offset = 0, int factor = 1): const expr {
        if (end == -1) {
            end = start;
        }
        start += offset;
        end += offset;
        expr res = 0;
        for (int index = start; index < end; index++) {
            res += air.CLK[factor*index];
        }
        return res * cl;
    }
}