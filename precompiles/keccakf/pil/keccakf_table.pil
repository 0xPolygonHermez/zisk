require "std_lookup.pil";
require "opids.pil";

const int KECCAKF_TABLE_SIZE = 145**3;

airtemplate KeccakfTable(const int N, const int group_by = 3) {
    // Ensure that the chosen size fits
    if (N < KECCAKF_TABLE_SIZE) {
        error(`N must be at least ${KECCAKF_TABLE_SIZE}, but N=${N} was provided`);
    }

    const int MAX_VALUE = 144; // Gotten from `keccakf_expr_generator.rs`
    const int BASE = MAX_VALUE + 1;

    int MAX = 0;
    for (int i = 0; i < group_by; i++) {
        MAX += BASE**i * MAX_VALUE;
    }

    col fixed A = [0..MAX]...;

    col fixed C[group_by];
    #pragma transpile
    for (int i = 0; i < N; i++) {
        for (int j = 0; j < group_by; j++) {
            const int aj = (A[i] / (BASE**j)) % BASE;
            C[j][i] = aj % 2;
        }
    }

    col witness multiplicity;
    lookup_proves(KECCAKF_TABLE_ID, [A, ...C], multiplicity);
}