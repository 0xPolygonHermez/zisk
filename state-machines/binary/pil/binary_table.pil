require "std_constants.pil";
require "std_lookup.pil"

// PIL Binary Operations Table used by Binary
//                                                                                                     Running Total
// MINU/MINU_W  (OP:0x02)       2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN) x 2^1 (RESULT_IS_A)     = 2^19 |                     2^19
// MIN/MIN_W    (OP:0x03)       2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN) x 2^1 (RESULT_IS_A)     = 2^19 |              2^20
// MAXU/MAXU_W  (OP:0x04)       2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN) x 2^1 (RESULT_IS_A)     = 2^19 |              2^20 + 2^19
// MAX/MAX_W    (OP:0x05)       2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN) x 2^1 (RESULT_IS_A)     = 2^19 |       2^21
// LT_ABS_NP    (OP:0x06)   *   2^16 (AxB) x 2^1 (LAST) x 2^2 (CIN)                         = 2^19 |       2^21        + 2^19
// LT_ABS_PN    (OP:0x07)   *   2^16 (AxB) x 2^1 (LAST) x 2^2 (CIN)                         = 2^19 |       2^21 + 2^20
// LTU/LTU_W    (OP:0x08)   *   2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN)                         = 2^18 |       2^21 + 2^20        + 2^18
// LT/LT_W      (OP:0x09)   *   2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN)                         = 2^18 |       2^21 + 2^20 + 2^19
// GT           (OP:0x0a)   *   2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN)                         = 2^18 |       2^21 + 2^20 + 2^19 + 2^18
// EQ/EQ_W      (OP:0x0b)   *   2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN)                         = 2^18 | 2^22
// ADD/ADD_W    (OP:0x0c)       2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN)                         = 2^18 | 2^22                     + 2^18
// SUB/SUB_W    (OP:0x0d)       2^16 (AxB) x 2^1 (LAST) x 2^1 (CIN)                         = 2^18 | 2^22              + 2^19
// LEU/LEU_W    (OP:0x0e)   *   2^16 (AxB) x 2^1 (LAST)                                     = 2^17 | 2^22              + 2^19        + 2^17
// LE/LE_W      (OP:0x0f)   *   2^16 (AxB) x 2^1 (LAST)                                     = 2^17 | 2^22              + 2^19 + 2^18
// AND/AND_W    (OP:0x10)   **  2^16 (AxB) x 2^1 (LAST)                                     = 2^17 | 2^22              + 2^19 + 2^18 + 2^17
// OR/OR_W      (OP:0x11)   **  2^16 (AxB) x 2^1 (LAST)                                     = 2^17 | 2^22       + 2^20
// XOR/XOR_W    (OP:0x12)   **  2^16 (AxB) x 2^1 (LAST)                                     = 2^17 | 2^22       + 2^20               + 2^17
// EXT_32       (OP:0x13)   *** 2^8  (A)                x 2^1 (CIN) x 2^2 (FLAGS)           = 2^11 | 2^22       + 2^20               + 2^17 + 2^11 => < 2^23
// --------------------------------------------------------------------------------------------------------------------------
// (*) Uses the carry of the last byte of the result (use_last_carry)
// (**) The op do not use LAST, but the binary does so we need to consider it
// (***) The op do not use CIN, but the binary does so we need to consider it
// Note: EXT_32 is the only unary operation which is not a ZisK OP but it is used to prove the rest

const int BINARY_TABLE_ID = 125;

const int EXT_32_OP = 0x13;

const int INITIAL_CARRY_LT_ABS = 0x02;

airtemplate BinaryTable(const int N = 2**23, const int disable_fixed = 0) {

    #pragma memory m1 start
    col witness multiplicity;

    if (disable_fixed) {
        col fixed _K = [0...];
        // FORCE ONE TRACE
        multiplicity * _K === 0;

        println("*** DISABLE_FIXED ***");
        return;
    }

    if (N < 2**23) {
        error(`N must be at least 2^23, but N=${N} was provided`);
    }

    #pragma timer tt start
    #pragma timer t1 start

    // Input A (8 bits)
    col fixed A = [0..255]...;                          

    // Input B (<=8 bits)
    col fixed B = [[0:P2_8..255:P2_8]:82, // 82 = 4*8 + 2*8 + 4*4 + 2*4 + 2*2 + 3*2
                    0:P2_11]...;          // B is 0 for EXT_32

    // Indicator of the last byte (<=1 bit)
    col fixed LAST = [[0:P2_16, 1:P2_16]:(4*4), // MINU,MIN,MAXU,MAX
                      [0:P2_16, 1:P2_16]:(2*4), // LT_ABS_NP,LT_ABS_PN
                      [0:P2_16, 1:P2_16]:(4*2), // LTU,LT,GT,EQ
                      [0:P2_16, 1:P2_16]:(2*2), // ADD,SUB
                      [0:P2_16, 1:P2_16]:2,     // LEU,LE
                      [0:P2_16, 1:P2_16]:3,     // AND,OR,XOR
                       0:P2_11]...;

    // Input carry (<=2 bits)
    col fixed CIN = [[0:P2_17, 1:P2_17]:(4*2), // MINU,MIN,MAXU,MAX
                     [0:P2_17..3:P2_17]:2,     // LT_ABS_NP,LT_ABS_PN
                     [0:P2_17, 1:P2_17]:4,     // LTU,LT,GT,EQ
                     [0:P2_17, 1:P2_17]:2,     // ADD,SUB
                      0:(P2_17*2),             // LEU,LE
                      0:(P2_17*3),             // AND,OR,XOR
                     [0:P2_8, 1:P2_8]:4]...;   // EXT_32

    // Operation opcode (fixed values)
    col fixed OP = [0x02:P2_19, 0x03:P2_19, 0x04:P2_19, 0x05:P2_19, // MINU,MIN,MAXU,MAX
                    0x06:P2_19, 0x07:P2_19,                         // LT_ABS_NP,LT_ABS_PN
                    0x08:P2_18, 0x09:P2_18, 0x0a:P2_18, 0x0b:P2_18, // LTU,LT,GT,EQ
                    0x0c:P2_18, 0x0d:P2_18,                         // ADD,SUB
                    0x0e:P2_17, 0x0f:P2_17,                         // LEU,LE
                    0x10:P2_17, 0x11:P2_17, 0x12:P2_17,             // AND,OR,XOR
                    0x13:P2_11]...;                                 // EXT_32

    const int TABLE_SIZE = P2_19 * 6 + P2_18 * 6 + P2_17 * 5 + P2_11;
    const int TABLE_BASE_EXT32 = P2_16 * 82;

    #pragma timer t1 end
    #pragma timer t2 start

    col fixed C;             // Output C (8 bits)
    col fixed FLAGS;

    #pragma transpile logfile:/tmp/binary_table_2.txt
    for (int i = 0; i < N; i++) {
        const int [plast, op, a, b, cin] = [LAST[i], OP[i], A[i], B[i], CIN[i]];
        int c = 0;
        int cout = 0;
        int op_is_min_max = 0;
        int use_last_carry = 0;
        int index = i % TABLE_SIZE;
        int result_is_a = index < P2_21 ? ((index >> 18) & 0x01) : 0;
        switch (op) {
            case 0x02,0x03: // MINU,MINU_W,MIN,MIN_W
                // cout = 1 indicates that a is lower than b
                if (a < b) {
                    cout = 1;
                } else if (a == b) {
                    cout = cin;
                }

                if (result_is_a) {
                    c = a;
                } else {
                    c = b;
                }

                if (op == 0x03 && plast) {
                    if ((a & 0x80) != (b & 0x80)) {
                        cout = (a & 0x80) ? 1 : 0;
                    }
                }

                op_is_min_max = 1;

            case 0x04,0x05: // MAXU,MAXU_W,MAX,MAX_W
                // cout = 1 indicates that a is greater than b
                if (a > b) {
                    cout = 1;
                } else if (a == b) {
                    cout = cin;
                }

                if (result_is_a) {
                    c = a;
                } else {
                    c = b;
                }

                if (op == 0x05 && plast) {
                    if ((a & 0x80) != (b & 0x80)) {
                        cout = (a & 0x80) ? 0 : 1;
                    }
                }
                op_is_min_max = 1;

            case 0x06: // LT_ABS_NP
                // Both necessary carries are encoded by cin in binary as 
                //                  cin = 0bYX,
                // where X is the carry of the LT operation and Y is 
                // the carry of the operation a ^ 0xFF + _cop

                // Decode the carries
                const int _clt = cin & 0x01;
                const int _cop = (cin & 0x02) >> 1;

                const int _a = (a ^ 0xFF) + _cop; // _cop should be 1 at the first byte and _a >> 8 at the rest 
                const int _b = b;    

                if ((_a  & 0xFF) < _b) {
                    cout = 1;
                    c = plast;
                } else if ((_a  & 0xFF) == _b) {
                    cout = _clt;
                    c = plast * _clt;
                }

                // Encode the result carries
                cout += 2*(_a >> 8);

                use_last_carry = plast;

            case 0x07: // LT_ABS_PN
                // Both necessary carries are encoded by cin in binary as 
                //                  cin = 0bYX,
                // where X is the carry of the LT operation and Y is 
                // the carry of the operation b ^ 0xFF + _cop

                // Decode the carries
                const int _clt = cin & 0x01;
                const int _cop = (cin & 0x02) >> 1;

                const int _a = a;
                const int _b = (b ^ 0xFF) + _cop; // _cop should be 1 at the first byte and _b >> 8 at the rest              

                if (_a < (_b & 0xFF)) {
                    cout = 1;
                    c = plast;
                } else if (_a == (_b & 0xFF)) {
                    cout = _clt;
                    c = plast * _clt;
                }

                // Encode the result carries
                cout += 2*(_b >> 8);

                use_last_carry = plast;

            case 0x08,0x09: // LTU,LTU_W,LT,LT_W
                if (a < b) {
                    cout = 1;
                    c = plast;
                } else if (a == b) {
                    cout = cin;
                    c = plast * cin;
                }

                // If the chunk is signed, then the result is the sign of a
                if (op == 0x09 && plast && (a & 0x80) != (b & 0x80)) {
                    c = (a & 0x80) ? 1 : 0;
                    cout = c;
                }

                use_last_carry = plast;

            case 0x0a: // GT
                if (a > b) {
                    cout = 1;
                    c = plast;
                } else if (a == b) {
                    cout = cin;
                    c = plast * cin;
                }

                // The result is the sign of b
                if (plast && (a & 0x80) != (b & 0x80)) {
                    c = (b & 0x80) ? 1 : 0;
                    cout = c;
                }

                use_last_carry = plast;

            case 0x0b: // EQ,EQ_W
                if (a == b && !cin) c = plast;
                else cout = 1;
                if (plast) cout = 1 - cout;

                use_last_carry = plast;

            case 0x0c: // ADD,ADD_W
                c = (cin + a + b) & 0xFF;
                cout = plast ? 0 : (cin + a + b) >> 8;

            case 0x0d: // SUB,SUB_W
                sign = (a - cin) >= b ? 0 : 1;
                c = 256 * sign + a - cin - b;
                cout = plast ? 0 : sign;

            case 0x0e,0x0f: // LEU,LEU_W,LE,LE_W
                if (a <= b) {
                    cout = 1;
                    c = plast;
                }

                if (op == 0x0f && plast && (a & 0x80) != (b & 0x80)) {
                    c = (a & 0x80) ? 1 : 0;
                    cout = c;
                }

                use_last_carry = plast;

            case 0x10: // AND
                c = a & b;

            case 0x11: // OR
                c = a | b;

            case 0x12: // XOR
                c = a ^ b;

            case 0x13: // EXT_32
                c = (a & 0x80) ? 0xFF : 0x00;
                const int index_offset = (index - TABLE_BASE_EXT32) >> 9;
                op_is_min_max = index_offset & 0x01;
                if ((index_offset & 0x03) == 0x03) {
                    result_is_a = 1;
                } else {
                    result_is_a = 0;
                }

            default:
                error(`Invalid operation opcode: ${op} in row ${i}`);
        }
        C[i] = c;
        const int flags = cout + 2*op_is_min_max + 4*result_is_a + 8*use_last_carry;
        FLAGS[i] = flags;
        log(`T ${plast},${op},${a},${b},${cin},${c},${flags},${i}`);
    }
    #pragma timer t2 end
    #pragma timer tt end

    lookup_proves(BINARY_TABLE_ID, [LAST, OP, A, B, CIN, C, FLAGS], multiplicity);
    #pragma memory m1 end
}