require "std_lookup.pil"
require "std_permutation.pil"
require "std_direct.pil"

const int BOOT_ADDR = 0x1000;
const int END_PC_ADDR = 0x1000_0000;
const int INITIAL_SP = 0;
const int END_SP = 0;
const int MAX_RANGE = 1 << 24;
const int REG_BASE_ADDR = 0xA000_0000;

airtemplate Main(int N = 2**21, int RC = 2, int stack_enabled = 0, const int operation_bus_id, const int MAIN_CONTINUATION_ID = 1000, const int REG_NUM = 7) {

    col fixed SEGMENT_L1 = [1,0...];
    col fixed SEGMENT_STEP = [0..(N-1)];

    const expr SEGMENT_LAST = SEGMENT_L1';

    // CONTINUATIONS
    //
    // Main continuations use air values to define constraints in a one-dimensional space
    // between air segments. Additionally, constraints involving these air values
    // and two-dimensional elements (trace, with rows and columns) are defined in air,
    // usually affecting values in the first and last rows of each segment.
    //
    //                       ┌── segment/next_segment
    //                       │   ┌── 0/is_last_segment
    //                       │   │     ┌── inicial_pc/next_pc
    //                       │   │     │          ┌── previous_c/last_c
    //                       │   │     │          │
    //                       │   │     │          │             BUS
    //  global constraint  [  0|  0|INITIAL_PC|0         ]   => | | ╌┐
    //                                                          | |  ┆
    //  segment-0          [  0|  0|INITIAL_PC|0         ]   <= | | <┘
    //  segment-0          [  1|  0|pc_1      |last_c    ]   => | | ╌┐
    //  segment-1          [  1|  0|pc_1      |previous_c]   <= | | <┘
    //  segment-1          [  2|  0|pc_2      |last_c    ]   => | | ╌┐
    //                     :   :   :          :          :      : :  :
    //                     :   :   :          :          :      : :  :
    //  segment-i          [  i|  0|pc_i      |previous_c]   <= | | <┘
    //  segment-i          [i+1|  0|pc_i+1    |last_c    ]   => | | ╌┐
    //                     :   :   :          :          :      : :  :
    //                     :   :   :          :          :      : :  :
    //  segment-n          [  n|  0|pc_n      |previous_c]   <= | | <┘
    //  segment-n          [  0|  1|END_PC    |0         ]   => | | ╌┐
    //                                                          | |  ┆
    //  global constraint  [  0|  1|END_PC    |0         ]   <= | | <┘
    //
    // One segment "assumes" the state/values of the previous segment and "proves" the state/values
    // of the next segment. To prevent an insecure cycle, we enforce that the beginning and end
    // of the cycle are defined by global constraints. With this mechanism, we ensure that only one
    // cycle is executed and what is exactly the initial and final state of the main trace.
    //
    // In the "assumed" state of the segment we put a 0 on second column (is_last_segment), while in
    // "proved" state we put the value of is_last_segment, with this we ensure that the end of last
    // segment cannot be used to init a new cycle.
    //
    // In main continuations, the `pc` of the next step is determined by the current state
    // (flags, register values, etc.). For this reason, in the last row, the next `pc` value is
    // calculated and then sent to the bus. In the first row of the next segment, it is verified
    // that these two values match.
    //
    // In the case of `c`, the current value of `c` is sent in the last row of a segment.
    // Within a segment, when `last_c` is referenced, if we are in the first row,
    // we take the value received from the bus; otherwise, we take the value from the previous row.

    airval main_last_segment;
    airval main_segment;
    airval segment_initial_pc;
    airval segment_previous_c[RC];
    airval segment_next_pc;
    airval segment_last_c[RC];

    if (stack_enabled) {
        airval segment_initial_sp;
        airval segment_next_sp;
    }

    // boolean constraint

    main_last_segment * (1 - main_last_segment) === 0;

    const expr STEP = main_segment * N + SEGMENT_STEP;

    // Registers

    col witness a[RC];
    col witness b[RC];
    col witness c[RC];
    col witness flag;                  // Conditional flag modified by operations

    col witness pc;                    // Program counter

    // Source A

    col witness a_src_imm;             // Selector indicating that 'a' uses an immediate value.
    col witness a_src_mem;             // Selector indicating that 'a' is read from memory.
    col witness a_offset_imm0;         // Depending on the flag values, this witness contains either
                                       // the lower 32-bit part of an immediate value or the memory
                                       // address offset.
    if (stack_enabled == 1) {
        col witness air.sp;
        col witness air.a_src_sp;
        col witness air.a_use_sp_imm1;
    } else {
        col witness air.a_imm1;        // The higher 32-bit part of a immediate value
    }
    col witness a_src_step;            // Selector to load the `step` in register `a`

    // Source B

    col witness b_src_imm;             // Selector indicating that 'b' uses an immediate value.
    col witness b_src_mem;             // Selector indicating that 'b' is read from memory.
    col witness b_offset_imm0;         // Depending on the flag values, this witness contains either
                                       // the lower 32-bit part of an immediate value or the memory
                                       // address offset.

    if (stack_enabled) {
        col witness air.b_use_sp_imm1;
    } else {
        col witness air.b_imm1;        // The higher 32-bit part of a immediate value
    }
    col witness b_src_ind;             // Selector to indicate take value in a as address to
                                       // read b form memory (b = mem[a])
    col witness ind_width;             // bytes: 8, 4, 2, 1

    // Operations related

    col witness is_external_op;        // [is_external_op == 1] => operation op sent to bus
    col witness op;                    // [is_external_op == 0 && op=0] ==>  a=last_c, flag=1
                                       // [is_external_op == 0 && op=0] ==>  b=last_c, flag=0
                                       // else then this op is sent to the bus.

    // Destination C

    // What to store
    col witness store_ra;              // Store the return address instead of the C register.

    // Where to store
    col witness store_mem;             // Selector for store c to memory
    col witness store_ind;             // Selector for store c to memory address in a

    col witness store_offset;          // Memory address offset used on c

    col witness set_pc;                // Selector to set to pc the value in c


    if (stack_enabled) {
        col witness air.store_use_sp;
        col witness air.set_sp;
        col witness air.inc_sp;
    }


    col witness jmp_offset1;           // next default pc if flag = 0
    col witness jmp_offset2;           // next default pc if flag = 1
    col witness m32;                   // selector to indicate the high part of operands (a,b) send
                                       // to bus must be 0, because it's a 32 bits operation. Used
                                       // to avoid sent to bus dirty values.

    const expr sel_mem_b;
    sel_mem_b = b_src_mem + b_src_ind;

    // Some of these values are expressions while others are witness + constraint. The expressions
    // are "converted" to witness to avoid increase the degree on stage 2 with extension.

    if (stack_enabled) {
        col witness air.addr0;
        col witness air.addr1;
        col witness air.addr2;

        addr0 === a_offset_imm0 + a_use_sp_imm1 * sp;
        addr1 === b_offset_imm0 + b_src_ind * (a[0] + 2**32 * a[1]) +  b_use_sp_imm1 * sp;
        addr2 === store_offset + store_ind * a[0] + store_use_sp * sp;
    } else {
        const expr air.addr0;
        col witness air.addr1;
        const expr air.addr2;
        addr0 = a_offset_imm0;
        addr1 === b_offset_imm0 + b_src_ind * (a[0] + 2**32 * a[1]);
        addr2 = store_offset + store_ind * a[0];
    }

    // regs on main
    col witness a_reg_prev_mem_step;
    col witness b_reg_prev_mem_step;
    col witness store_reg_prev_mem_step;
    col witness store_reg_prev_value[RC];
    col witness a_src_reg;
    col witness b_src_reg;
    col witness store_reg;

    const expr a_mem_step = main_step_to_mem_step(STEP, 0);
    const expr b_mem_step = main_step_to_mem_step(STEP, 1);
    const expr store_mem_step = main_step_to_mem_step(STEP, 2);

    // memory function to define the first memory read slot.
    reg_pre_load(sel: a_src_reg,
             prev_mem_step: a_reg_prev_mem_step,
             addr: addr0,
             value: a);

    mem_load(sel: a_src_mem + a_src_reg,
             mem_step: a_mem_step,
             addr: addr0,
             value: a);


    // memory function to define the second memory read slot.
    reg_pre_load(sel: a_src_reg,
             prev_mem_step: b_reg_prev_mem_step,
             addr: addr1,
             value: b);

    mem_load(sel: sel_mem_b + b_src_reg,
             mem_step: b_mem_step,
             bytes: b_src_ind * (ind_width - 8) + 8,
             addr: addr1,
             value: b);


    const expr store_value[2];

    store_value[0] = store_ra*(pc + jmp_offset2 - c[0]) + c[0];
    store_value[1] = (1 - store_ra) * c[1];

    // memory function to define the first memory store slot (after memory reads)
    mem_store(sel: store_mem + store_ind + store_reg,
              mem_step: store_mem_step,
              bytes: store_ind * (ind_width - 8) + 8,
              addr: addr2,
              value: store_value);

    reg_pre_store(sel: store_reg,
              prev_mem_step: store_reg_prev_mem_step,
              addr: store_offset,
              value: store_reg_prev_value);

    range_check(a_mem_step - a_reg_prev_mem_step, 1, MAX_RANGE);
    range_check(b_mem_step - b_reg_prev_mem_step, 1, MAX_RANGE);
    range_check(store_mem_step - store_reg_prev_mem_step, 1, MAX_RANGE);

    lookup_assumes(operation_bus_id, [op, a[0], (1 - m32) * a[1], b[0], (1 - m32) * b[1], ...c, flag], sel: is_external_op);

    const expr a_src_c;
    const expr b_src_c;

    const expr a_imm[2];
    const expr b_imm[2];

    a_imm[0] = a_offset_imm0;
    b_imm[0] = b_offset_imm0;

    if (stack_enabled) {
        a_src_c = 1 - a_src_step - a_src_mem - a_src_imm - a_src_sp;
        b_src_c = 1 - b_src_mem - b_src_imm - b_src_ind;
        a_imm[1] = a_use_sp_imm1;
        b_imm[1] = b_use_sp_imm1;
    } else {
        a_src_c = 1 - a_src_step - a_src_mem - a_src_imm;
        b_src_c = 1 - b_src_mem - b_src_imm - b_src_ind;
        a_imm[1] = a_imm1;
        b_imm[1] = b_imm1;
    }


    for (int index = 0; index < RC; ++index) {
        const expr previous_c = SEGMENT_L1 * (segment_previous_c[index] - 'c[index]) + 'c[index];

        if (stack_enabled) {
            // a_src_sp ===> set a = sp (only less significant index, rest must be 0)
            a_src_sp * (a[index] - (index == 0 ? sp: 0 )) === 0;
        }
        a_src_step * (a[index] - (index == 0 ? STEP : 0)) === 0;
        a_src_c * (a[index] - previous_c) === 0;
        b_src_c * (b[index] - previous_c) === 0;

        a_src_imm * (a[index] - a_imm[index]) === 0;
        b_src_imm * (b[index] - b_imm[index]) === 0;

        // continuations_transition(last_c[index], c[index]); // last_c[index]' = c[index];

        // if is not an external op and op=0 set c = 0
        (1 - is_external_op) * (1 - op) * c[index] === 0;

        // if is not an external op and op=1 set c = b
        (1 - is_external_op) * op * (b[index] - c[index]) === 0;
    }


    // if is not an external op and op=0 set flag = 1
    (1 - is_external_op) * (1 - op) * (1 - flag) === 0;

    // if is not an external op and op=1 set flag = 0
    (1 - is_external_op) * op * (flag) === 0;

    flag * set_pc === 0;

    const expr expected_current_pc = 'set_pc * ('c[0] + 'jmp_offset1) + (1 - 'set_pc) * ('pc + 'jmp_offset2) + 'flag * ('jmp_offset1 - 'jmp_offset2);
    (1 - SEGMENT_L1) * (pc - expected_current_pc) === 0;


    if (stack_enabled) {
        const expr expected_current_sp = 'set_sp * ('c[0] - 'sp) + 'sp + 'inc_sp;
        const expr next_sp = expected_current_sp';

        SEGMENT_L1 * (sp - segment_initial_sp) === 0;
        (1 - SEGMENT_L1) * (sp - expected_current_sp) === 0;
        SEGMENT_LAST * (next_sp - segment_next_sp) === 0;
    }

    const expr next_pc = expected_current_pc';
    SEGMENT_LAST * (segment_next_pc - next_pc) === 0;

    for (int index = 0; index < RC; ++index) {
        SEGMENT_LAST * (segment_last_c[index] - c[index]) === 0;
    }

    // Register Zero is hard-wired zero.
    airval last_reg_value[REG_NUM][RC];
    airval last_reg_mem_step[REG_NUM];

    const expr last_segment_reg_mem_step = main_step_to_special_mem_step(main_segment * N + N - 1);
    for (int ireg = 0; ireg < REG_NUM; ++ireg) {
        range_check(expression: last_segment_reg_mem_step - last_reg_mem_step[ireg], min: 1, max: MEMORY_MAX_DIFF);
        reg_pre_load(prev_mem_step: last_reg_mem_step[ireg], addr: ireg + 1, value: last_reg_value[ireg]);
        mem_load(mem_step: last_segment_reg_mem_step, addr: ireg + 1, value: last_reg_value[ireg], sel: (1 - main_last_segment));
    }

    flag * (1 - flag) === 0;



    // set lsb of rom_flags always to 1 to force that padding rom rows (all values to zero), doesn't
    // match with main trace.
    const expr rom_flags = 1 + 2 * a_src_imm + 4 * a_src_mem + 8 * a_src_step + 16 * b_src_imm
                           + 32 * b_src_mem + 64 * is_external_op + 128 * store_ra + 256 * store_mem
                           + 512 * store_ind + 1024 * set_pc + 2048 * m32 + 4096 * b_src_ind
                           + 8192 * a_src_reg + 16384 * b_src_reg + 32768 * store_reg;


    lookup_assumes(ROM_BUS_ID, [pc, a_offset_imm0, a_imm1, b_offset_imm0, b_imm1, ind_width,
                                op, store_offset, jmp_offset1, jmp_offset2, rom_flags]);

    // Continuations

    // In the last segment

    const expr main_next_segment = (main_segment + 1) * (1 - main_last_segment);

    if (stack_enabled) {
        direct_update_assumes(MAIN_CONTINUATION_ID, [main_segment, 0, segment_initial_pc, ...segment_previous_c, segment_initial_sp]);
        direct_update_proves(MAIN_CONTINUATION_ID, [main_next_segment, main_last_segment, segment_next_pc, ...segment_last_c, segment_next_sp]);
    } else {
        direct_update_assumes(MAIN_CONTINUATION_ID, [main_segment, 0, segment_initial_pc, ...segment_previous_c]);
        direct_update_proves(MAIN_CONTINUATION_ID, [main_next_segment, main_last_segment, segment_next_pc, ...segment_last_c]);
    }

    // This global constraint is sent to the bus to define the initial and final values of
    // continuations. A global constraint is used to ensure there is only one initial and one final
    // value, preventing multiple cycles of `main`.
    // In the initial state define the initial PC and final PC. Before finish a execution the
    // value of "c" must be 0.

    const int zeros[RC];
    for (int index = 0; index < RC; ++index) {
        zeros[index] = 0;
    }

    if (stack_enabled) {
        direct_global_update_proves(MAIN_CONTINUATION_ID, expressions: [0, 0, BOOT_ADDR, ...zeros, INITIAL_SP]);
        direct_global_update_assumes(MAIN_CONTINUATION_ID, expressions: [0, 1, END_PC_ADDR, ...zeros, END_SP]);
    } else {
        direct_global_update_proves(MAIN_CONTINUATION_ID, expressions: [0, 0, BOOT_ADDR, ...zeros]);
        direct_global_update_assumes(MAIN_CONTINUATION_ID, expressions: [0, 1, END_PC_ADDR, ...zeros]);
    }

    for (int ireg = 0; ireg < REG_NUM; ++ireg) {
        global_init_mem(sel: 1, addr: reg_to_addr(ireg), value: zeros);
    }

}

function reg_to_addr (int reg_index): int {
    return REG_BASE_ADDR + reg_index * 8;
}
